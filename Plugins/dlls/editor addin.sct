<scriptlet>

<implements type="Automation" id="dispatcher">
	<property name="PluginEvent">
		<get/>
	</property>
	<property name="PluginDescription">
		<get/>
	</property>
	<property name="PluginExtendedProperties">
		<get/>
	</property>
	<method name="MakeUpper"/>
	<method name="MakeLower"/>
	<method name="SortAscending"/>
	<method name="SortDescending"/>
	<method name="ExecFilterCommand"/>
</implements>

<script language="VBS">
Option Explicit

Const WinMergeRegKeyPath = "HKCU\Software\Thingamahoochie\WinMerge\"
Const PluginRegKeyPath = "HKCU\Software\Thingamahoochie\WinMerge\Plugins\editor addin.sct\"

Dim wsh: Set wsh = CreateObject("WScript.Shell")

Function get_PluginEvent()
         get_PluginEvent = "EDITOR_SCRIPT"
End Function

Function get_PluginDescription()
         get_PluginDescription = "Basic text functions for the context menu"
End Function

Function get_PluginExtendedProperties()
	get_PluginExtendedProperties = "GenerateUnpacker;MakeUpper.MenuCaption=Make uppercase;MakeLower.MenuCaption=Make lowercase;SortAscending.MenuCaption=Sort ascending;SortDescending.MenuCaption=Sort descending;ExecFilterCommand.MenuCaption=Execute filter command"
End Function

Function regRead(Key, DefaultValue)
	regRead = DefaultValue
	On Error Resume Next
	regRead = wsh.RegRead(Key)
End Function

Function IsLangJapanese()
	Dim languageId
	On Error Resume Next
	languageId = CLng(regRead(WinMergeRegKeyPath & "Locale\LanguageId", 1033))
	IsLangJapanese = (languageId = 1041)
End Function

' transformation functions
Function MakeUpper(Text)
	MakeUpper = UCase(Text)
End Function

Function MakeLower(Text)
	MakeLower = LCase(Text)
End Function

Function ExecFilterCommand(Text)
	Dim cmd
	cmd = regRead(PluginRegKeyPath & "ExecFilterCommand", "")
	If IsLangJapanese() Then
		cmd = InputBox("フィルタコマンドを入力してください", "ExecFilterCommand", cmd)
	Else
		cmd = InputBox("Enter filter command", "ExecFilterCommand", cmd)
	End If
	If cmd = "" Then
		If IsLangJapanese() Then
			Err.Raise 30001, , "キャンセルされました"
		Else
			Err.Raise 30001, , "Canceled"
		End If
		Exit Function
	End If

	On Error Resume Next

	Dim path
	path = wsh.ExpandEnvironmentStrings("%TEMP%\_winmerge_addin_temp_.txt")

	Dim fso
	Dim ts
	Set fso = CreateObject("Scripting.FileSystemObject")
	Set ts = fso.CreateTextFile(path)
	If ts Is Nothing Then
		Exit Function
	End If
	ts.Write Text
	ts.Close

	Dim exe
	Set exe = wsh.Exec("cmd /c type """ & path & """  | " & cmd & " 2>&1")
	If exe Is Nothing Then
		If IsLangJapanese() Then
			MsgBox "コマンド " & cmd & " の実行に失敗しました:" & Err.Description 
		Else
			MsgBox "Failed to execute the command '" & cmd & "':" & Err.Description 
		End If
		fso.DeleteFile path
		Exit Function
	End If

	ExecFilterCommand = ""
	Do Until exe.StdOut.AtEndOfStream
		ExecFilterCommand = ExecFilterCommand & exe.StdOut.ReadLine & vbCrLf
	Loop

	fso.DeleteFile path

	wsh.RegWrite PluginRegKeyPath & "ExecFilterCommand", cmd

End Function


</script>

<script language="JScript">

function SortAscending(Text) {
	var eol = Text.match(/\r\n|\n|\r/);
	var lines = Text.split(eol);

	if (lines.length == 1) {
		return Text;
	} else if (lines[lines.length - 1] == "") {
		lines.pop();
		return lines.sort().join(eol) + eol;
	} else {
		return lines.sort().join(eol);
	}
}

function SortDescending(Text) {
	var eol = Text.match(/\r\n|\n|\r/);
	var lines = Text.split(eol);

	if (lines.length == 1) {
		return Text;
	} else if (lines[lines.length - 1] == "") {
		lines.pop();
		lines.sort(function(a, b) { return a < b ? 1 : -1; });
		return lines.join(eol) + eol;
	} else {
		return lines.sort(function(a, b) { return a < b ? 1 : -1; }).join(eol);
	}
}

</script>

</scriptlet>
